plugins {
    id 'net.minecraftforge.gradle'
    id 'java'
    id 'eclipse'
    id 'org.cadixdev.licenser'
    id 'org.spongepowered.mixin'
}

group = 'com.craftingdead'
version = "${mod_version}"

minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
	mavenCentral()
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    library
    implementation.extendsFrom library
    shadow.extendsFrom library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name:'forge', version: "${minecraft_version}-${forge_version}"
    implementation group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
    testAnnotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
    implementation 'io.sentry:sentry:5.6.0'
    implementation 'io.sentry:sentry-log4j2:5.6.0'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

jar {
    archiveBaseName = project.name
    archiveVersion = "${minecraft_version}-${version}"
    from rootProject.file('LICENSE.txt')
    finalizedBy('reobfJar') 
}

license {
    include '**/*.java'

    header = rootProject.file('HEADER.txt')

    ext {
        organization = 'NexusNode LTD'
        year = 2021
    }
}    