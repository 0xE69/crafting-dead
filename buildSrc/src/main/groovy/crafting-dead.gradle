plugins {
    id 'net.minecraftforge.gradle'
    id 'java'
    id 'eclipse'
    id 'org.cadixdev.licenser'
    id 'org.spongepowered.mixin'
    id 'com.github.johnrengelman.shadow'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

group = 'com.craftingdead'
version = "${mod_version}"

minecraft {
    mappings channel: 'official', version: "${minecraft_version}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
	mavenCentral()
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    library
    implementation.extendsFrom library
    
    shaded_library
    shaded.extendsFrom shaded_library
    library.extendsFrom shaded_library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name:'forge', version: "${minecraft_version}-${forge_version}"
    implementation group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
    testAnnotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

reobf {
    shadowJar { }
}

shadowJar {
    configurations = [project.configurations.shaded]
    archiveVersion = "${minecraft_version}-${version}"
    archiveClassifier = ''
    finalizedBy 'reobfShadowJar'
}

jar {
    archiveBaseName = project.name
    archiveVersion = "${minecraft_version}-${version}"
    from rootProject.file('LICENSE.txt')
    finalizedBy('reobfJar') 
}

license {
    include '**/*.java'

    header = rootProject.file('HEADER.txt')

    ext {
        organization = 'NexusNode LTD'
        year = 2021
    }
}    