buildscript {
    repositories {
        maven { 
        	name 'forge'
        	url 'https://files.minecraftforge.net/maven'
		}
		maven {
			name 'sponge'
			url 'https://dist.creeper.host/Sponge/maven'
		}
        jcenter()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forge_gradle_version}"
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
	id 'java-library'
	id 'eclipse'
	id 'org.cadixdev.licenser' version '0.5.0'
}

subprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'org.spongepowered.mixin'
	apply plugin: 'eclipse'
	apply plugin: 'org.cadixdev.licenser'

	sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

    group = 'com.craftingdead'
    version = "${mod_version}"
	
	minecraft {
    	mappings channel: "${mappings_channel}", version: "${mappings_version}"
    }
    
    repositories {
	    jcenter()
	    maven {
	    	name 'sponge'
	    	url 'https://dist.creeper.host/Sponge/maven'
		}
	}

	configurations {
		implementation.extendsFrom shaded
	}

    dependencies {
    	minecraft group: 'net.minecraftforge', name:'forge', version: "${minecraft_version}-${forge_version}"
		implementation group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
		annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
	}
	
	jar.finalizedBy('reobfJar') 
	
	jar {
	    baseName = project.name
	    appendix = minecraft_version
	    from {
			configurations.shaded.collect { it.isDirectory() ? it : zipTree(it) }
		}
		from rootProject.file('LICENSE.txt')
	}

	license {
		include '**/*.java'
		
	    header = rootProject.file('HEADER.txt')

	    ext {
	        organization = 'NexusNode LTD'
	        year = 2021
	    }
	}
}
