repositories {
	maven {
        name 'cursemaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    google()
}

dependencies {
    implementation fg.deobf("curse.maven:travelers_backpack-${travelers_backpack_project_id}:${travelers_backpack_file_id}")
}

jar {
    manifest {
        attributes([
        	"Implementation-Title": project.name,
        	"Implementation-Version": project.version,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": 'craftingdead.mixins.json'
        ])
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
	add sourceSets.main, "craftingdead.refmap.json"
}

minecraft {    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runCoreClient'
 
            workingDirectory project.file('client')

            property 'forge.logging.console.level', 'debug'
            
            args '--mixin', 'craftingdead.mixins.json'
            
            mods {
                craftingdead {
                    source sourceSets.main
                }
            }
        }

        server {
            taskName 'runCoreServer'
        
            workingDirectory project.file('server')

            property 'forge.logging.console.level', 'debug'
            
            args '--mixin', 'craftingdead.mixins.json'
            
            mods {
                craftingdead {
                    source sourceSets.main
                }
            }
        }
        
        data {
            taskName 'runCoreData'
      
            workingDirectory project.file('data')

            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'craftingdead', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            environment 'target', 'fmluserdevdata'

            mods {
                craftingdead {
                    source sourceSets.main
                }
            }
        }
    }
}
