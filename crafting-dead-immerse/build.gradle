repositories {
	mavenCentral()
	maven {
		name 'jitpack'
		url 'https://jitpack.io' 
	}
	maven {
      	url 'http://maven.ferenyr.info/artifactory/ourten'
    }
    google()
    maven {
    	url 'https://maven.enginehub.org/repo'
    }
}

jar {
    manifest {
        attributes([
        	"Implementation-Title": project.name,
        	"Implementation-Version": project.version,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": 'craftingdeadimmerse.mixins.json'
        ])
    }
}

mixin {
	add sourceSets.main, "craftingdeadimmerse.refmap.json"
}

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
        	taskName 'runImmerseClient'
        
            workingDirectory project.file('client')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            
            args '--mixin', 'craftingdeadimmerse.mixins.json', '--mixin', 'craftingdead.mixins.json'

            mods {
                craftingdeadimmerse {
                    source sourceSets.main
                }
                craftingdead {
                    source project(':crafting-dead-core').sourceSets.main
                }
            }
        }

        server {
        	taskName 'runImmerseServer'
        
            workingDirectory project.file('server')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            
            args '--mixin', 'craftingdeadimmerse.mixins.json'

            mods {
                craftingdeadimmerse {
                    source sourceSets.main
                }
                craftingdead {
                    source project(':crafting-dead-core').sourceSets.main
                }
            }
        }
    }
}

dependencies {
    shaded group: 'io.noties', name: 'tumbleweed', version: '2.1.0'
	compileOnly project(':crafting-dead-core')
	shaded group: 'org.lwjgl', name: 'lwjgl-yoga', version: "${lwjgl_version}"
	shaded "org.lwjgl:lwjgl-yoga::${lwjgl_natives}"
}

eclipse {
    classpath {
    	// Temp hack - see https://github.com/eclipse/buildship/issues/939
        plusConfigurations += [ configurations.compileOnly ]
   	}
}