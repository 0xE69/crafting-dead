plugins {
    id 'crafting-dead'
}

repositories {
    mavenCentral()
    google()
    maven {
   	    url 'https://packages.jetbrains.team/maven/p/skija/maven'   	
    }
    flatDir {
        dir 'libs'
    }
}

mixin {
    add sourceSets.main, 'craftingdeadimmerse.refmap.json'
}

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runImmerseClient'

            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mixin', 'craftingdeadimmerse.mixins.json', '--mixin', 'craftingdead.mixins.json'

            mods {
                craftingdeadimmerse {
                    source sourceSets.main
                }
                craftingdead {
                    source project(':crafting-dead-core').sourceSets.main
                }
            }
        }

        server {
            taskName 'runImmerseServer'

            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mixin', 'craftingdeadimmerse.mixins.json', '--mixin', 'craftingdead.mixins.json'

            mods {
                craftingdeadimmerse {
                    source sourceSets.main
                }
                craftingdead {
                    source project(':crafting-dead-core').sourceSets.main
                }
            }
        }
    }
}

configurations {
    library
    implementation.extendsFrom library
    shadow.extendsFrom library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

def lwjgl_natives = ['natives-windows', 'natives-linux', 'natives-macos']

dependencies {
    implementation project(':crafting-dead-core')
    library (group: 'org.lwjgl', name: 'lwjgl-yoga', version: "${lwjgl_version}") {
        exclude group: 'org.lwjgl', module: 'lwjgl'
    }
    lwjgl_natives.each {
        library ("org.lwjgl:lwjgl-yoga::${it}") {
            exclude group: 'org.lwjgl', module: 'lwjgl'
        }
    }

	library group: 'net.java.timingframework', name: 'timingframework-core', version: '7.3.1'
}

jar {
    manifest {
        attributes([
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'MixinConfigs': 'craftingdeadimmerse.mixins.json'
        ])
    }
}
